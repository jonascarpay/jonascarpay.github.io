---
title: Managing secrets with `agenix`
date: 2021-07-27
abstract:
  A practical guide on securely managing and deploying secrets with `agenix`
tags: nix agenix secrets cryptography age
---

It's considered bad practice to put sensitive information directly into your Nix configuration.
The primary concern is that _anything Nix touches ends up in the Nix store_, and the store is readable to everything and everyone on your computer.
Furthermore, you might want to share your configuration with other people, which now obviously becomes impossible.
Does that mean you will just have to distribute your secrets through other channels?
There are actually [many](https://nixos.wiki/wiki/Comparison_of_secret_managing_schemes) proposed solutions to this issue, but I generally found them to not be worth the hassle and used to just recommend setting up your secrets manually instead.

That is, until I discovered [`agenix`](https://github.com/ryantm/agenix).
`agenix` requires almost no setup or overhead, and yet works seamlessly across multiple machines and users.
We use it at `$WORK`, I [use it in my personal config](https://github.com/jonascarpay/nix/tree/master/secrets), and I've been very happy with it.

Unfortunately, I find that [the manual](https://github.com/ryantm/agenix#readme) makes `agenix` seem more complicated than it is, and doesn't really highlight what makes it so nice to use.
So, here is ~~more evangelism for niche technologies~~ a brief, opinionated guide to `agenix`.

### NixOS configuration

Start by importing the `agenix` module into your system configuration.
The most straightforward way is just importing the tarball directly[^tarball], but you can of course use your preferred method of dependency management here (niv, flakes, channels, etc.).

From here, the concept is simple.
You configure `agenix` with an encrypted file, you ask/specify where the decrypted file should go, and then _at boot time_ the file gets decrypted and put in the promised location.

#### Example

As an example, let's say we're setting up an OpenVPN configuration:
```nix
{
  # Option 1
  services.openvpn.myServer.config = ''
    auth-user-pass path/to/credentials.txt
  '';
  # Option 2
  services.openvpn.myServer.config = ''
    auth-user-pass ${path/to/credentials.txt}
  '';
}
```
Option 1 avoids the nix store, but it is not declarative; we need to manually make sure that the specified file actually exists.

With option 2, we have nix manage the file for us, but now it ends up in the nix store, making it world-readable.

With `agenix`, we get the best of both worlds:
```nix
{
  age.secrets.myServerCredentials.file = path/to/credentials.age;
  services.openvpn.myServer.config = ''
    auth-user-pass ${config.age.secrets.myServerCredentials.path}
  '';
}
```
The only thing nix ever sees is the encrypted `.age` file, so no unencrypted secrets will touch the nix store.
By default, `config.age.secrets.myServerCredentials.path` will evaluate to `/run/secrets/myServerCredentials`.
Early during boot our file gets decrypted and put there.

That's all there is to it in terms of configuration.
Of course, this still leaves questions like how you actually encrypt your file, or how the system decrypts the file.

### Working with secrets

`agenix` is powered by [`age`](https://github.com/FiloSottile/age).
`age` doesn't have many features[^good], but it has two that make it ideal for this use case: multiple recipients, and encryption using SSH keys.

You have an SSH key, and your systems each have SSH keys[^sshd][^info].
Yours is the one typically found in `~/.ssh/`, and the one for your system is in `/etc/ssh/` or reported by `ssh-keyscan localhost`.
This is perfect because (again) `age` can use SSH keys for encryption, but more importantly these are _exactly_ the recipients of secrets: you, because you want to view/edit secrets, and your systems, because they need to decrypt the file at boot.
In other words, all the cryptographic infrastructure `agenix` uses is already in place!
This is why there was so little configuration required in the previous step, and fortunately this also extends to actually setting up/working with our secrets.

Let's see how we actually encrypt a file.

1. Start by making a folder in your nix config called `secrets`.
This folder will contain your secrets, as well as a `secrets.nix` file.

2. In the `secrets` folder, create your `secrets.nix` file.
`secrets.nix` determines what files are encrypted with what keys.
While `secrets.nix` is a nix file, it is not in any way actually part of the rest of your nix configuration, you never import it anywhere.
It is only used when encrypting secrets (which we will do in the next step), it dictates what files can be viewed with what public keys.
This is the example `secrets.nix` file from [the tutorial](https://github.com/ryantm/agenix#tutorial):
```nix
let
  user1 = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIL0idNvgGiucWgup/mP78zyC23uFjYq0evcWdjGQUaBH";
  user2 = "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABAQCoQ9S7V+CufAgwoehnf2TqsJ9LTsu8pUA3FgpS2mdVwcMcTs++8P5sQcXHLtDmNLpWN4k7NQgxaY1oXy5e25x/4VhXaJXWEt3luSw+Phv/PB2+aGLvqCUirsLTAD2r7ieMhd/pcVf/HlhNUQgnO1mupdbDyqZoGD/uCcJiYav8i/V7nJWJouHA8yq31XS2yqXp9m3VC7UZZHzUsVJA9Us5YqF0hKYeaGruIHR2bwoDF9ZFMss5t6/pzxMljU/ccYwvvRDdI7WX4o4+zLuZ6RWvsU6LGbbb0pQdB72tlV41fSefwFsk4JRdKbyV3Xjf25pV4IXOTcqhy+4JTB/jXxrF";
  users = [ user1 user2 ];

  system1 = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIPJDyIr/FSz1cJdcoW69R+NrWzwGK/+3gJpqD1t8L2zE";
  system2 = "ssh-ed25519 AAAAC3NzaC1lZDI1NTE5AAAAIKzxQgondgEYcLpcPdJLrTdNgZ2gznOHCAxMdaceTUT1";
  systems = [ system1 system2 ];
in
{
  "secret1.age".publicKeys = [ user1 system1 ];
  "secret2.age".publicKeys = users ++ systems;
}
```

3. Use `agenix -e <filename>` to edit files.
One thing to note is that the system module doesn't install the `agenix` executable -- you still need to get that somewhere.
In my flake-based config it's just
```nix
{ environment.systemPackages = [ inputs.agenix.defaultPackage."${system}" ]; }
```

That's it!
You now have secure declarative management of secrets.
The only other thing to be aware of is that you need to rekey your secrets if you change what files have access to them, which is a matter of running `agenix --rekey`.

### Caveats

#### Configuration

One thing to watch out for is that incorporating your secrets in your config is not always as easy as it is in the OpenVPN example above.
You might want to use secrets for options that were not designed to be kept secret, or use `agenix` for things that aren't configured through nix at all.

Two examples from my own config:

1. I want to add secret binary caches, but the `nix.binaryCaches` option only takes strings, it does not take file paths.
That means there is no way of using the option without it ending up in the nix store.
So, I sidestep the usual cache declaration mechanism and [go through `nix.extraOptions` and the new `!include` directive](https://github.com/jonascarpay/nix/blob/0d6fde33df85fe1b97a0fda79299ff4096c38f3d/system/xc-cache.nix#L4).

2. AWS credentials aren't configured through nix, it relies on a file being present in a designated location in your root/user folder.
So, we just override the target path so it [writes to `/root/.aws/credentials` directly](https://github.com/jonascarpay/nix/blob/0d6fde33df85fe1b97a0fda79299ff4096c38f3d/system/xc-cache.nix#L8).

The takeaway is that sometimes it just requires some creativity, but it is not unimaginable that you have secrets for which `agenix` just does not work out.
As far as I know this is mostly just an issue with nix itself, and applies to all secret management solutions.

#### Some thoughts on security

I am not a security expert, the author of `agenix` [is not a security expert](https://github.com/ryantm/agenix#threat-modelwarnings), so if you're like me, maybe you're a bit hesitant of trusting `agenix` with your secrets.
I'm not here to convince you of anything, but here is some research that you might find helpful.

First of all, the [author of `age` itself _is_ a security expert](https://github.com/FiloSottile).
I found [some discussion of _potential_ attack vectors against `age`](https://neilmadden.blog/2019/12/30/a-few-comments-on-age/), but based on _my understanding_ of [the underlying reasoning](https://moxie.org/2011/12/13/the-cryptographic-doom-principle.html), this would not apply to `agenix` since it's not high-volume network traffic.

Second, you can very easily audit `agenix` yourself, it's just [150 lines of pretty understandable bash in the encryption code](https://github.com/ryantm/agenix/blob/master/pkgs/agenix.nix), and [another 100 in the nix module](https://github.com/ryantm/agenix/blob/master/modules/age.nix).

For what it's worth, I trust it enough to make my encrypted secrets (including work-related secrets) public and then brag about it in a blog post, so at least if something breaks we're both screwed.

[^tarball]: 
```nix
{ imports = [ "${builtins.fetchTarball "https://github.com/ryantm/agenix/archive/master.tar.gz"}/modules/age" ]; }
```
[^good]: A good thing for a cryptographic application!
[^sshd]: Provided you have ssh, i.e. `services.openssh`, enabled. If not, you manually have to specify `age.sshKeyPaths`.
[^info]: More info on the distinction between user and system keys [here](https://unix.stackexchange.com/questions/439467/what-is-the-difference-between-etc-ssh-and-ssh).
